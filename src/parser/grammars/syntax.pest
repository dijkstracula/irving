COMMENT = _{ !SOI ~ "#" ~ !"lang " ~ (!"\n" ~ ANY)* }

// A param is a token with an optional type annotation.  These are for things
// like function argument lists.
param = { symbol ~ ( ":" ~ ident)? }
paramlist = { param ~ ("," ~ param)* }

// Expressions

// https://pest.rs/book/precedence.html
expr            =  { atom ~ (infix_op ~ atom)* }
atom            = _{ prefix_op? ~ primary ~ postfix_op? }
    prefix_op   = _{ UMINUS | NOT }
    infix_op    = _{ PLUS | MINUS | TIMES | DIV |
                     LT | LE | GT | GE | EQ | NEQ |
                     AND | OR | DOT }
    postfix_op  = _{ fnapp_args | index }
primary         = _{ THIS | symbol | number | boollit | "(" ~ expr ~ ")" }

fnapp_args = { "(" ~ (expr ~ ("," ~ expr)* )? ~ ")" }
//TODO: only one dimension of indexing, right?
index      = { "[" ~ expr ~ "]" }

// Types

enum_decl  = { "{" ~ symbol ~ ("," ~ symbol)* ~ "}" }
range_decl = { "{" ~ expr ~ ".." ~ expr ~ "}" }

// Decls

decl_ret   = { "returns" ~ "(" ~ param ~ ")" } //TODO: needs to be a paramlist
decl_sig =  { symbol ~ ("(" ~ paramlist ~ ")")? }
decl_block = { "{" ~ (decl ~ ";"?)* ~ "}" }

mod_sig = { symbol ~ ( "(" ~ symbol ~ ("," ~ symbol)? ~ ")")? }

include_decl      = { INCLUDE ~ symbol }
var_decl          = { VAR ~ term }

attribute_decl    = { ATTRIBUTE ~ expr } // TODO: wildly overspecified: shoudl be constrained beyond just an expr.

alias_decl        = { ALIAS ~ symbol ~ "=" ~ expr }
axiom_decl        = { AXIOM ~ fmla }

function_decl     = { FUNCTION ~ decl_sig ~ ":" ~ symbol }
relation_decl     = { RELATION ~ decl_sig }

global_decl       = { GLOBAL ~ decl_block }
isolate_decl       = { ISOLATE ~ decl_sig ~ "=" ~ decl_block }
object_decl       = { OBJECT ~ decl_sig ~ "=" ~ decl_block }

import_decl       = { IMPORT ~ ACTION ~ decl_sig }
export_decl       = { EXPORT ~ (symbol | action_decl) }

module_decl       = { MODULE ~ mod_sig ~ "=" ~ decl_block }

instance_decl     = { INSTANCE ~ symbol ~ ":" ~ mixin_sig }

implementation_decl    = { IMPLEMENTATION ~ decl_block }
specification_decl     = { SPECIFICATION ~ decl_block }
common_decl            = { COMMON ~ decl_block }

// TODO: struct decl
type_name         = { symbol | THIS }
type_decl         = { TYPE ~ type_name ~ (("=" ~ (range_decl | enum_decl)) | ("of" ~ symbol))? } // superclass might have to be more than a symbol

invariant_decl    = { INVARIANT ~ fmla }

// TODO: not sure exactly about these ones yet.
header_decl       = { HEADER ~ symbol }
interpret_decl    = { INTERPRET ~ symbol }
instantiate_decl  = { INSTANTIATE ~ symbol }
autoinstance_decl = { AUTOINSTANCE ~ decl_sig }

decl = {
      action_decl
    
    | implementation_decl
    | specification_decl
    | common_decl
    | global_decl

    | alias_decl
    | attribute_decl
    | axiom_decl
    | function_decl
    | relation_decl
    | include_decl
    | var_decl
    | header_decl
    | implement_action_decl
    | instance_decl
    | interpret_decl
    | invariant_decl
    | isolate_decl
    | instantiate_decl
    | module_decl
    | object_decl
    | instance_decl
    | autoinstance_decl
    | import_decl
    | export_decl
    | after_decl
    | before_decl
    | type_decl

    | (stmt ~ (";" ~ stmt)* )
}

// Actions and mixins
action_decl       = { ACTION ~ decl_sig ~ decl_ret? ~ ("=" ~ stmt_block)? }
after_decl        = { AFTER  ~ (mixin_sig ~ decl_ret?)? ~ stmt_block }
before_decl       = { BEFORE ~ (mixin_sig ~ decl_ret?)? ~ stmt_block }
implement_action_decl    = { IMPLEMENT ~ mixin_sig ~ decl_ret? ~ stmt_block }

// NB: Because a mixin signature can refer to an action outside the current scope, the sig
// name needs to be a qualified identifier and not just a symbol token.
mixin_sig =  { ident ~ ("(" ~ paramlist ~ ")")? }

assign_action = { (var_decl | expr) ~ ":=" ~ expr} //TODO
assert_action = { ASSERT ~ expr}
assume_action = { ASSUME ~ expr}
call_action =   { expr }
ensure_action = { ENSURE ~ fmla }
requires_action = { REQUIRE ~ fmla }

action = {
      assign_action
    | assert_action
    | assume_action
    | call_action
    | ensure_action
    | requires_action
}

actions = { action ~ (";" ~ action)* }

// Statements

stmt_block = { "{" ~ (stmt ~ ";"?)* ~ "}" }

return_stmt = { "return" ~ expr}
if_stmt     = { "if"    ~ expr ~ stmt_block ~ ("else" ~ stmt_block)? }
while_stmt  = { "while" ~ expr ~ stmt_block }

stmt = {
      actions
    | if_stmt
    | return_stmt
    | while_stmt
    | var_decl // or something??  Feels weird to mix a decl into a stmt like this.
}

// Toplevels 
hashlang    = _{ SOI ~ "#lang" ~ langver }
    langver = ${ "ivy" ~ number ~ ("." ~ number)? }

prog = {
      hashlang
    ~ decl*
    ~ EOI
}
