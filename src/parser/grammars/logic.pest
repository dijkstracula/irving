// Grammar for first-order logic rules.

LOGICVAR = @{ !reserved ~ ASCII_ALPHA_UPPER ~ ("_" | ASCII_ALPHANUMERIC)* }

// aka simplevar in ivy_logic_utils
// TODO: The sort ought to be an ident, I think
// https://github.com/dijkstracula/irving/issues/27
logicvar         = { LOGICVAR ~ (":" ~ symbol)? }

fmla             =  { 
    exists | forall | log_term
}

exists           =  { EXISTS ~ logicvar ~ ("," ~ logicvar)* ~ "." ~ fmla }
forall           =  { FORALL ~ logicvar ~ ("," ~ logicvar)* ~ "." ~ fmla }

log_subscr      =  { symbol ~ ("." ~ symbol)* }

log_term        =  { log_atom ~ (log_infix_op ~ log_atom)* }
log_atom        = _{ log_prefix_op? ~ log_primary ~ log_app_args? }
    log_prefix_op   = _{ NOT }
    log_infix_op    = _{ IFF | ARROW |
                     LE | LT | GE | GT | EQ | NEQ |
                     AND | OR | DOT }
log_primary    = _{ logicvar | symbol | boollit | number | "(" ~ log_term ~ ")" }

log_app_args    = { "(" ~ log_term ~ ("," ~ log_term)* ~ ")" }